plugins {
	id 'java'
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'com.gradleup.shadow' version '9.0.0-beta4'
	id 'io.freefair.lombok' version '8.11'
	id 'com.github.gmazzo.buildconfig' version '5.5.1'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.mod_id
}

repositories {
	maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org/releases"
	}
	maven {
		name = "meteor-maven"
		url = "https://maven.meteordev.org"
	}
	maven { url = 'https://maven.ladysnake.org/releases' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	implementation(shadow("org.reflections:reflections:0.10.2"))
	implementation(shadow("meteordevelopment:discord-ipc:1.1"))
	implementation 'com.github.scribejava:scribejava-apis:8.3.1'
	implementation 'com.github.scribejava:scribejava-core:8.3.1'
	implementation 'net.java.dev.jna:jna:5.13.0'
	implementation 'net.java.dev.jna:jna-platform:5.13.0'
	implementation 'se.michaelthelin.spotify:spotify-web-api-java:8.3.0'
	modImplementation 'io.github.ladysnake:satin:3.0.0-alpha.1'
	implementation 'org.apache.commons:commons-text:1.11.0'
	implementation 'org.json:json:20231013'
}

def getCommitHash = { ->
	def gitProcess = 'git rev-parse HEAD'.execute()
	gitProcess.waitFor()
	return gitProcess.text.trim().take(10)
}

def getRevisionCount = { ->
	def gitProcess = 'git rev-list --all --count'.execute()
	gitProcess.waitFor()
	return gitProcess.text.trim()
}


buildConfig {
	className("BuildConstants")
	packageName(group)

	buildConfigField(String, 'MOD_NAME', project.mod_name)
	buildConfigField(String, 'MOD_ID', project.mod_id)
	buildConfigField(String, 'MOD_VERSION', project.mod_version)
	buildConfigField(String, 'MINECRAFT_VERSION', project.minecraft_version)
	buildConfigField(String, 'GIT_HASH', getCommitHash())
	buildConfigField(String, 'GIT_REVISION', getRevisionCount())
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

loom {
	accessWidenerPath = file("src/main/resources/lyrica.accesswidener")
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier.set("dev")
}

remapJar {
	dependsOn(shadowJar)
	inputFile = tasks.shadowJar.archiveFile
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/libs/libs"
    include '*.jar'
}